plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def innerVersion = '1.2'
group = 'com.github.billtom20'
version = "${innerVersion}"

def clientId() {
    Properties properties = new Properties()
    try {
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    } catch (ignored) {
        // Ignore
    }
    return properties.getProperty("google.client_id_prefix", "invalid")
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "String", "GOOGLE_CLIENT_ID", "\"${clientId()}\""
        manifestPlaceholders = [
                redirectScheme: "com.googleusercontent.apps.${clientId()}"
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "com.squareup.okio:okio:2.10.0"
    implementation "joda-time:joda-time:2.10.10"
    implementation "commons-codec:commons-codec:1.11"

    api 'net.openid:appauth:0.11.1'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.billtom20'
                artifactId = 'GAppAuth'
                version = "${innerVersion}"
            }
        }
    }
}